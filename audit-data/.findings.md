### [H-1] Erroneous `ThunderLoan::updateExchangeRate` in the `deposit` function causese protocol to think it has more fees than it really does, which blocks redemption incorrectly sets the exchange rate.

**Description** In the Thunderloan system,the `exchangeRate` is responsible for calculating the exchange rate between assetTokens and underlying tokens. In a way, it's responsible for keeping track of how many fees to give liqudity providers.

However, the `deposit` function, updates this rate, without collecting any fees~

```javascript

    function deposit(IERC20 token, uint256 amount) external revertIfZero(amount) revertIfNotAllowedToken(token) {
        AssetToken assetToken = s_tokenToAssetToken[token];
        uint256 exchangeRate = assetToken.getExchangeRate();
        uint256 mintAmount = (amount * assetToken.EXCHANGE_RATE_PRECISION()) / exchangeRate;
        emit Deposit(msg.sender, token, amount);
        assetToken.mint(msg.sender, mintAmount);
        uint256 calculatedFee = getCalculatedFee(token, amount);
@>        assetToken.updateExchangeRate(calculatedFee);
        token.safeTransferFrom(msg.sender, address(assetToken), amount);
    }
```

**Impact** There are several impacts to this bug.

1. The `redeem` function is blocked, because the protocol thingks the owed tokens is more than it has.
2. Rewars are incorrectly calculated, leading to liquidity proivders potentially getting way more or less than deserved.

**Proof of Concept**

1. LP deposits
2. User takes out a flash loan
3. It is now impossible for LP to redeem.

<details>
<summary>Proof of Code</summary>

Place the following into `ThunderLoanTest.t.sol`.

```javascript
    function testRedeemAfterLoan() public setAllowedToken hasDeposits{
        uint256 amountToBorrow = AMOUNT * 10;
        uint256 calculatedFee = thunderLoan.getCalculatedFee(tokenA, amountToBorrow);

        vm.startPrank(user);
        tokenA.mint(address(mockFlashLoanReceiver), calculatedFee); //
        thunderLoan.flashloan(address(mockFlashLoanReceiver), tokenA, amountToBorrow, "");
        vm.stopPrank();

        uint256 amountToRedeem = type(uint256).max;
        vm.startPrank(liquidityProvider);
        thunderLoan.redeem(tokenA, amountToRedeem);
    }
```
<details>

**Recommended Mitigations** Remove the incorrectly updated exchange rate lines from `deposit`.


```diff

    function deposit(IERC20 token, uint256 amount) external revertIfZero(amount) revertIfNotAllowedToken(token) {
        AssetToken assetToken = s_tokenToAssetToken[token];
        uint256 exchangeRate = assetToken.getExchangeRate();
        uint256 mintAmount = (amount * assetToken.EXCHANGE_RATE_PRECISION()) / exchangeRate;
        emit Deposit(msg.sender, token, amount);
        assetToken.mint(msg.sender, mintAmount);
-       uint256 calculatedFee = getCalculatedFee(token, amount);
-       assetToken.updateExchangeRate(calculatedFee);
        token.safeTransferFrom(msg.sender, address(assetToken), amount);
    }
    
```

### [H-2] Mixng up variable locatino causes storage collisions in `ThunderLoan::s_flashLoanFee` and `ThunderLoan::s_currentlyFlashLoaning`, freezing protocol.

**Description** `ThunderLoan.sol` has two variables in the following order.

```javascript
    uint256 private s_feePrecision;
    uint256 private s_flashLoanFee; // 0.3% ETH fee
```

However, the upgraded contract `ThunderLoandUpgraded.sol` has them in a different order:

```javascript
    uint256 private s_flashLoanFee; // 0.3% ETH fee
    uint256 public constant FEE_PRECISION = 1e18;
```

Due to how solidity works, after the upgrade the `s_flashLoanFee` will have the avlue of `s_feePrecision`. You cannot adjust the positino of storage variables, and removing storage varabiles for constant variables, beraks the storage locations as well.

**Impact** After the ugprade, the `s_flashLoanFee` will have the value of `s_feePrecision`. This means that users who take out flash loans right after an upgrade will be charged the wrong fee. 

More importanly the `s_currentlyFlashLoaning` mapping will start in the wrong storage slot. 

**Proof of Concept**

<details>
<summary>PoC</summary>

Place the following into `ThunderLoanTest.t.sol`.

```javascript
import {ThunderLoanUpgraded} from "../../src/upgradedProtocol/ThunderLoanUpgraded.sol";
...

    function testUpgradeBreaks() public {
        uint256 feeBeforeUpgrade = thunderLoan.getFee();
        vm.startPrank(thunderLoan.owner());
        ThunderLoanUpgraded upgraded = new ThunderLoanUpgraded();
        thunderLoan.upgradeToAndCall(address(upgraded), "");
        uint256 feeAfterUpgarde = thunderLoan.getFee();

        console2.log("Fee before: ", feeBeforeUpgrade);
        console2.log("Fee after: ", feeAfterUpgarde);
        assert(feeBeforeUpgrade != feeAfterUpgarde);
    }

```
You can also see the storage layout difference by running `forge inspect ThunderLoan storage` and `forge inspect THunderLoanUpgarded storage`

<details>

**Recommended Mitigations** If you must remove the storage variable, leave it as blank as to not mess up the storage slots.

```diff
-    uint256 private s_flashLoanFee; // 0.3% ETH fee;
-    uint256 public constant FEE_PRECISION = 1e18;
+    uint256 private s_blank; // 0.3% ETH fee;
+    uint256 private s_flashLoanFee; // 0.3% ETH fee;
+    uint256 public constant FEE_PRECISION = 1e18;
```